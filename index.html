<html>
    <head>
        <style>
            body {
                margin: 0;
                background-color: white;
            }
            canvas {
                display: block;  /* prevents scrollbar */
                width: 100vw;
                height: 100vh;
            }
            #gui { 
                position: absolute;
                left: 10px;
                top: 10px;
                font-family: monospace;
            }
        </style>
    </head>
    <body>
        <script type="text/javascript" src="./dat.gui/build/dat.gui.js"></script>
        <canvas id="canvas"></canvas>
        <script src="./myUtils.js"></script>
        <script src="./matrixUtils.js"></script>
        <script src="./perlinNoise.js"></script>
        <script src="./mesh.js"></script>
        <script src="./shaderUtils.js"></script>
        <script src="./physics.js"></script>
        <script src="./player.js"></script>
        <script src="./octree.js"></script>
        <script src="./chunks.js"></script>
        <script src="./world.js"></script>

        <script  id="vertex-shader-3d" type="x-shader/x-vertex">#version 300 es
            in vec3 aPosition;

            in vec4 aColor;
            in vec2 aTextCoord;
            uniform vec2 uTextureSize;
            uniform vec2 uAtlasSize;

            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;

            uniform vec2 uWorldPos;
            in float aData;

            out vec3 vFogDepth;

            out vec4 vWorldPosition;
            out vec3 vNormal;
            out vec4 vColor;
            out vec2 vTextCoord;

            vec3 getNormal(float index) {
                if(index == 0.0) return vec3(0, 1, 0);    // top    +Y
                if(index == 1.0) return vec3(0, 0, 1);    // front  +Z
                if(index == 2.0) return vec3(-1, 0, 0);   // left   -X
                if(index == 3.0) return vec3(1, 0, 0);    // right  +X
                if(index == 4.0) return vec3(0, 0, -1);   // back   -Z
                if(index == 5.0) return vec3(0, -1, 0);   // bottom -Y
                return vec3(0, 0, 0);
            }

            mat3 getTextureMatrix(vec2 offset, vec2 size, vec2 atlasSize) {
                return mat3(
                    size.x / atlasSize.x,   0.0,                    0.0,
                    0.0,                    size.y / atlasSize.y,   0.0,
                    offset.x / atlasSize.x, offset.y / atlasSize.y, 1.0
                );
            }

            // Rotation matrices
            mat4 xRotationMatrix(float angle) {
                return mat4(
                    1.0, 0.0, 0.0, 0.0,
                    0.0, cos(angle), sin(angle), 0.0,
                    0.0, -sin(angle), cos(angle), 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            mat4 yRotationMatrix(float angle) {
                return mat4(
                    cos(angle), 0.0, -sin(angle), 0.0,
                    0.0, 1.0, 0.0, 0.0,
                    sin(angle), 0.0, cos(angle), 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            mat4 zRotationMatrix(float angle) {
                return mat4(
                    cos(angle), sin(angle), 0.0, 0.0,
                    -sin(angle), cos(angle), 0.0, 0.0,
                    0.0, 0.0, 1.0, 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            // Get rotation matrix based on face index
            mat4 getFaceRotation(float faceIndex) {
                const float PI = 3.14159265359;
                if(faceIndex == 0.0) return xRotationMatrix(PI);                                          // top   +Y
                else if(faceIndex == 1.0) return xRotationMatrix(-PI / 2.0);                              // front +Z 
                else if(faceIndex == 2.0) return zRotationMatrix(-PI / 2.0) * yRotationMatrix(-PI / 2.0); // left  -X 
                else if(faceIndex == 3.0) return zRotationMatrix(PI / 2.0) * yRotationMatrix(PI / 2.0);   // right +X
                else if(faceIndex == 4.0) return xRotationMatrix(PI / 2.0) * yRotationMatrix(PI);         // back  -Z
                return mat4(1.0); // bottom (faceIndex == 5.0)                                                     -Y
            }

            void main() {
                int intData = int(aData);

                float posX =       float(intData & 31);
                float posY =       float((intData >> 5) & 31);
                float posZ =       float((intData >> 10) & 31);
                float aNormal =    float((intData >> 15) & 7);
                float aTextureId = float((intData >> 18) & 15);

                vec3 pos = vec3(posX, posY, posZ);
                // Create translation matrix
                mat4 translationMatrix = mat4(
                    1.0, 0.0, 0.0, 0.0,
                    0.0, 1.0, 0.0, 0.0,
                    0.0, 0.0, 1.0, 0.0,
                    pos.x + 0.5 + uWorldPos.x, pos.y + 0.5, pos.z + 0.5 + uWorldPos.y, 1.0
                );

                mat4 rotationMatrix = getFaceRotation(aNormal);

                // Apply face rotation
                mat4 positionMatrix = translationMatrix * rotationMatrix;

                vWorldPosition = positionMatrix * vec4(aPosition, 1.0);
                
                gl_Position = uPmatrix * uVmatrix * vWorldPosition;

                vNormal = getNormal(aNormal);
                
                mat3 textureMatrix = getTextureMatrix(vec2(aNormal * uTextureSize.x, aTextureId * uTextureSize.y), uTextureSize, uAtlasSize);
                vTextCoord = (textureMatrix * vec3(aTextCoord, 1.0)).xy;

                // vColor = vec4(abs(vNormal), 1.); // * aColor; // change this to see colors *.*

                vFogDepth = (uVmatrix * vWorldPosition).xyz;
            }
        </script>
           
        <script  id="fragment-shader-3d" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            // The texture
            uniform sampler2D uTexture;

            uniform vec3 uAmbientLight;
            uniform vec3 uLightColor;
            uniform vec3 uLightPosition;
            uniform vec3 uViewPosition;
            uniform vec3 uGammaCorrection;

            uniform vec4 uFogColor;
            uniform float uFogNear;
            uniform float uFogFar;

            in vec4 vWorldPosition;
            in vec3 vNormal;
            in vec4 vColor;
            in vec2 vTextCoord;
            in vec3 vFogDepth;

            out vec4 outColor;

            void main() {
                vec4 color = texture(uTexture, vTextCoord); // vColor; //

                vec3 normal = normalize(vNormal);

                vec3 lightDirection = normalize(uLightPosition - vec3(vWorldPosition));
                
                float nDotLight = max(dot(lightDirection, normal), 0.0);

                vec3 diffuse = uLightColor * nDotLight * color.rgb;
                vec3 ambient = uAmbientLight * color.rgb;

                // Specular light
                float specularStrength = 0.1;
                vec3 viewDir = normalize(uViewPosition - vec3(vWorldPosition));
                vec3 reflectDir = reflect(-lightDirection, normal);
                float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32.0);
                vec3 specular = specularStrength * spec * vec3(1.0, 1.0, 0.0);  // White specular light

                color = vec4(pow(diffuse + ambient + specular, uGammaCorrection), color.a);

                float fogAmount = smoothstep(uFogNear, uFogFar, length(vFogDepth));

                outColor = mix(color, uFogColor, fogAmount);
            }
        </script>

        <script id="picker-vertex-shader" type="x-shader/x-vertex">#version 300 es
            in vec4 aPosition;

            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;
            uniform mat4 uMmatrix;

            void main() {
                gl_Position = uPmatrix * uVmatrix * uMmatrix * aPosition;
            }
        </script>

        <script id="picker-fragment-shader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            uniform float uAlphaColor;

            out vec4 outColor;

            void main() {
                outColor = vec4(0.0, 0.0, 0.0, uAlphaColor);
            }
        </script>

        <script id="crosshair-vertex-shader" type="x-shader/x-vertex">#version 300 es
            in vec4 aPosition;
            in vec4 aColor;

            uniform mat4 uMatrix;

            out vec4 vColor;

            void main() {
                gl_Position = uMatrix * aPosition + vec4(0.0, 0.0, 0.0, 15.0);
                vColor = aColor;
            }
        </script>

        <script id="crosshair-fragment-shader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            in vec4 vColor;

            out vec4 outColor;

            void main() {
                outColor = vColor;
            }
        </script>

        <script id="hand-block-vertex-shader" type="x-shader/x-vertex">#version 300 es
            in vec4 aPosition;

            in vec2 aTextCoord;

            uniform mat4 uMmatrix;

            out vec2 vTextCoord;

            void main() {
                gl_Position = uMmatrix * aPosition;
                vTextCoord = aTextCoord;
            }
        </script>

        <script id="hand-block-fragment-shader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            uniform sampler2D uTexture;
            in vec2 vTextCoord;

            out vec4 outColor;

            void main() {
                outColor = texture(uTexture, vTextCoord) * vec4(0.6, 0.6, 0.6, 1.0);
            }
        </script>

        <script id="clouds-vertex-shader" type="x-shader/x-vertex">#version 300 es
            in vec4 aPosition;

            in vec2 aTextCoord;

            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;
            uniform mat4 uMmatrix;

            out vec2 vTextCoord;

            void main() {
                gl_Position = uPmatrix * uVmatrix * uMmatrix * aPosition;
                vTextCoord = aTextCoord;
            }
        </script>

        <script id="clouds-fragment-shader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            uniform sampler2D uTexture;
            in vec2 vTextCoord;

            out vec4 outColor;

            void main() {
                outColor = texture(uTexture, vTextCoord) * vec4(1.0, 1.0, 1.0, 0.2);
            }
        </script>

        <script src="./index.js"></script>
</html>