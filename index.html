<html>
    <head>
        <style>
            body {
                margin: 0;
                background-color: white;
            }
            canvas {
                display: block;  /* prevents scrollbar */
                width: 100vw;
                height: 100vh;
            }
            #gui { 
                position: absolute;
                left: 10px;
                top: 10px;
                font-family: monospace;
            }
        </style>
    </head>
    <body>
        <script type="text/javascript" src="./dat.gui/build/dat.gui.js"></script>
        <canvas id="canvas"></canvas>
        <script src="./myUtils.js"></script>
        <script src="./matrixUtils.js"></script>
        <script src="./perlinNoise.js"></script>
        <script src="./mesh.js"></script>
        <script src="./shaderUtils.js"></script>
        <script src="./physics.js"></script>
        <script src="./player.js"></script>
        <script src="./octree.js"></script>
        <script src="./chunks.js"></script>
        <script src="./world.js"></script>

        <script  id="vertex-shader-3d" type="x-shader/x-vertex">
            attribute vec3 aPosition;

            attribute vec4 aColor;
            attribute vec2 aTextCoord;
            attribute float aTextureId;
            uniform vec2 uTextureSize;
            uniform vec2 uAtlasSize;

            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;
            attribute float aMmatrix;

            attribute vec2 aWorldPos;

            varying vec3 vFogDepth;

            attribute float aNormal;

            varying vec4 vWorldPosition;
            varying vec3 vNormal;
            varying vec4 vColor;
            varying vec2 vTextCoord;

            vec3 getNormal(float index) {
                if(index == 0.0) return vec3(0, 1, 0);    // top
                if(index == 1.0) return vec3(0, 0, 1);    // front
                if(index == 2.0) return vec3(-1, 0, 0);   // left
                if(index == 3.0) return vec3(1, 0, 0);    // right
                if(index == 4.0) return vec3(0, 0, -1);   // back
                if(index == 5.0) return vec3(0, -1, 0);   // bottom
                return vec3(0, 0, 0);
            }

            mat3 getTextureMatrix(vec2 offset, vec2 size, vec2 atlasSize) {
                return mat3(
                    size.x / atlasSize.x,   0.0,                    0.0,
                    0.0,                    size.y / atlasSize.y,   0.0,
                    offset.x / atlasSize.x, offset.y / atlasSize.y, 1.0
                );
            }

            // Rotation matrices
            mat4 xRotationMatrix(float angle) {
                return mat4(
                    1.0, 0.0, 0.0, 0.0,
                    0.0, cos(angle), sin(angle), 0.0,
                    0.0, -sin(angle), cos(angle), 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            mat4 yRotationMatrix(float angle) {
                return mat4(
                    cos(angle), 0.0, -sin(angle), 0.0,
                    0.0, 1.0, 0.0, 0.0,
                    sin(angle), 0.0, cos(angle), 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            mat4 zRotationMatrix(float angle) {
                return mat4(
                    cos(angle), sin(angle), 0.0, 0.0,
                    -sin(angle), cos(angle), 0.0, 0.0,
                    0.0, 0.0, 1.0, 0.0,
                    0.0, 0.0, 0.0, 1.0
                );
            }

            // Get rotation matrix based on face index
            mat4 getFaceRotation(float faceIndex) {
                const float PI = 3.14159265359;
                
                if(faceIndex == 0.0) { // top
                    return xRotationMatrix(PI);
                }
                else if(faceIndex == 1.0) { // front
                    return xRotationMatrix(-PI / 2.0);
                }
                else if(faceIndex == 2.0) { // left
                    return zRotationMatrix(-PI / 2.0) * yRotationMatrix(-PI / 2.0);
                }
                else if(faceIndex == 3.0) { // right
                    return zRotationMatrix(PI / 2.0) * yRotationMatrix(PI / 2.0);
                }
                else if(faceIndex == 4.0) { // back
                    return xRotationMatrix(PI / 2.0) * yRotationMatrix(PI);
                }
                // // bottom (faceIndex == 5.0) - no rotation needed
                return mat4(1.0);
            }

            void main(void) { 
                
                float posX = floor(mod(aMmatrix / 32.0 / 32.0, 32.0));
                float posY = floor(mod(aMmatrix / 32.0, 32.0));
                float posZ = floor(mod(aMmatrix , 32.0));
                
                
                vec3 pos = vec3(posX, posY, posZ);
                // Create translation matrix
                mat4 translationMatrix = mat4(
                    1.0, 0.0, 0.0, 0.0,
                    0.0, 1.0, 0.0, 0.0,
                    0.0, 0.0, 1.0, 0.0,
                    pos.x + 0.5 + aWorldPos.x, pos.y + 0.5, pos.z + 0.5 + aWorldPos.y, 1.0
                );

                mat4 rotationMatrix = getFaceRotation(aNormal);

                // Apply face rotation
                mat4 positionMatrix = translationMatrix * rotationMatrix;
                
                vWorldPosition = positionMatrix*vec4(aPosition, 1.);
            
                gl_Position = uPmatrix*uVmatrix*vWorldPosition;

                vNormal = getNormal(aNormal);

                mat3 textureMatrix = getTextureMatrix(vec2(aNormal*uTextureSize.x, aTextureId*uTextureSize.y), uTextureSize, uAtlasSize);
                vTextCoord = (textureMatrix * vec3(aTextCoord, 1.)).xy;

                // vColor = vec4(abs(vNormal), 1.); // * aColor; // change this to see colors *.*

                vFogDepth = (uVmatrix*vWorldPosition).xyz;
            }
        </script>
           
        <script  id="fragment-shader-3d" type="x-shader/x-fragment">
            // fragment shaders don't have a default precision so we need
            // to pick one. mediump is a good default
            precision mediump float;

            // The texture.
            uniform sampler2D uTexture;

            uniform vec3 uAmbientLight;
            uniform vec3 uLightColor;
            uniform vec3 uLightPosition;
            uniform vec3 uViewPosition; 
            uniform vec3 uGammaCorrection;     

            uniform vec4 uFogColor;
            uniform float uFogNear;
            uniform float uFogFar;

            varying vec4 vWorldPosition;
            varying vec3 vNormal;
            varying vec4 vColor;
            varying vec2 vTextCoord;

            varying vec3 vFogDepth;

            void main(void) {

                vec4 color = texture2D(uTexture, vTextCoord); // vColor; //

                vec3 normal = normalize(vNormal);

                vec3 lightDirection = normalize(uLightPosition - vec3(vWorldPosition));

                float nDotLight = max(dot(lightDirection, normal), 0.0);

                vec3 diffuse = uLightColor * nDotLight * color.rgb;
                vec3 ambient = uAmbientLight * color.rgb;
                
                // Specular light
                float specularStrength = 0.1;
                vec3 viewDir = normalize(uViewPosition -  vec3(vWorldPosition));
                vec3 reflectDir = reflect(-lightDirection, normal);
                float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32.0);
                vec3 specular = specularStrength * spec * vec3(1.0, 1.0, 0.0);  // White specular light

                color = vec4(pow(diffuse + ambient + specular, uGammaCorrection), color.a);

                float fogAmount = smoothstep(uFogNear, uFogFar, length(vFogDepth));

                gl_FragColor = mix(color, uFogColor, fogAmount);  
            }
        </script>

        <script id="picker-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 aPosition;
            
            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;
            uniform mat4 uMmatrix;
            
            void main() {
              gl_Position =  uPmatrix * uVmatrix *uMmatrix* aPosition;
            }
        </script>

        <script id="picker-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;
        
            uniform float uAlphaColor;
            
            void main() {
                gl_FragColor = vec4(0, 0, 0, uAlphaColor);
            }
        </script>

        <script id="crosshair-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 aPosition;
            attribute vec4 aColor;
    
            uniform mat4 uMatrix;
            
            varying vec4 vColor;
            
            void main() {
              gl_Position = uMatrix * aPosition + vec4(0, 0, 0, 15);
    
              vColor = aColor;
            }
        </script>

        <script id="crosshair-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;
        
            varying vec4 vColor;
            
            void main() {
                gl_FragColor = vColor;
            }
        </script>

        <script id="hand-block-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 aPosition;
            
            attribute vec2 aTextCoord;
            
            uniform mat4 uMmatrix;
            
            varying vec2 vTextCoord;
            
            void main() {
              gl_Position =  uMmatrix* aPosition;
    
              vTextCoord = aTextCoord;
            }
        </script>

        <script id="hand-block-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;
            
            uniform sampler2D uTexture;
            varying vec2 vTextCoord;
            
            void main() {
                gl_FragColor = texture2D(uTexture, vTextCoord) * vec4(.75, .75, .75, 1);
            }
        </script>

        <script id="clouds-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 aPosition;
            
            attribute vec2 aTextCoord;
            
            uniform mat4 uPmatrix;
            uniform mat4 uVmatrix;
            uniform mat4 uMmatrix;
            
            varying vec2 vTextCoord;
            
            void main() {
              gl_Position =  uPmatrix * uVmatrix *uMmatrix* aPosition;
    
              vTextCoord = aTextCoord;
            }
        </script>

        <script id="clouds-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;
            
            uniform sampler2D uTexture;
            varying vec2 vTextCoord;
            
            void main() {
                gl_FragColor = texture2D(uTexture, vTextCoord)* vec4(1, 1, 1, 0.2);
            }
        </script>

        <script src="./index.js"></script>
</html>